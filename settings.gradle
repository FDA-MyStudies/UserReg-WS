buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "${artifactory_contextUrl}/plugins-release-no-proxy"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT")) {
            mavenLocal()
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }
        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

rootProject.name="UserReg-WS"

apply from: 'gradle/settings/parameters.gradle'

include ':distributions:Registration'

gradle.beforeProject { project ->
    if (!project.hasProperty("labkeyVersion")) {
        throw new GradleException("Please run Gradle with the provided init script ('-I init.gradle') to build outside of a standard LabKey enlistment")
    }

    project.repositories {
        // Maven Central first
        mavenCentral()
        maven {
            url "${project.artifactory_contextUrl}/ext-tools-local"
        }
        maven {
            // Use this repository when relying on release versions of the LabKey artifacts and their external dependencies
            url "${project.artifactory_contextUrl}/libs-release-no-proxy"
        }
        maven {
            // Use this repository when relying on snapshot versions of LabKey artifacts
            url "${project.artifactory_contextUrl}/libs-snapshot-no-proxy"
        }
    }
}
